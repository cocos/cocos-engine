// Copyright (c) 2017-2020 Xiamen Yaji Software Co., Ltd.
CCEffect %{
  techniques:
  - passes:
    - vert: sprite-vs:vert
      frag: sprite-fs:frag
      depthStencilState:
        depthTest: false
        depthWrite: false
      blendState:
        targets:
        - blend: true
          blendSrc: src_alpha
          blendDst: one_minus_src_alpha
          blendDstAlpha: one_minus_src_alpha
      rasterizerState:
        cullMode: none
      properties:
        alphaThreshold: { value: 0.5 }
}%

CCProgram sprite-vs %{
  precision highp float;
  #include <cc-global>
  #if SAMPLE_FROM_RT
    #include <common>
  #endif

  in vec3 a_position;
  in vec2 a_texCoord;
  in float a_batch_id;

  out vec4 color;
  out vec2 uv0;

  struct UILocalData {
    vec4 rotation;
    vec4 tilingOffset;
    vec4 color;
    vec3 translation;
    vec3 scale;
  };

  // #define CC_UI_COUNT_PER_DRAW (CC_DEVICE_MAX_VERTEX_UNIFORM_VECTORS - CC_EFFECT_USED_VERTEX_UNIFORM_VECTORS) / 4
  #define CC_UI_COUNT_PER_DRAW 16
  #pragma builtin(local)
  layout(set = 2, binding = 0) uniform CCUILocal {
    // xyz: translation w: color R.G
    // xyzw: quat rotation
    // xy: scale zw: color BA
    // xy: tiling zw: offset
    mat4 cc_local_data[CC_UI_COUNT_PER_DRAW];
  };

  void getUILocalData(out UILocalData o) {
    mat4 data = cc_local_data[int(a_batch_id)];
    vec4 data0 = data[0];
    vec4 data2 = data[2];
    float rg = data0.w;
    float r = floor(rg);

    o.rotation = data[1];
    o.tilingOffset = data[3];
    o.translation = data0.xyz;
    o.scale = vec3(data2.xy, 1.0);
    o.color = vec4(r * (1.0 / 255.0), rg - r, data2.zw);
  }

  vec3 VectorTransformQuat (vec3 v, vec4 Q) {
    return v + 2.0 * cross(Q.xyz, cross(Q.xyz, v) + Q.w * v);
  }

  vec4 vert () {
    UILocalData i;
    getUILocalData(i);

    vec4 pos = vec4(VectorTransformQuat(a_position * i.scale, i.rotation) + i.translation, 1.0);
    uv0 = a_texCoord * i.tilingOffset.xy + i.tilingOffset.zw;
    color = i.color;

    #if USE_PIXEL_ALIGNMENT
      pos = cc_matView * pos;
      pos.xyz = floor(pos.xyz);
      pos = cc_matProj * pos;
    #else
      pos = cc_matViewProj * pos;
    #endif

    #if SAMPLE_FROM_RT
      CC_HANDLE_RT_SAMPLE_FLIP(uv0);
    #endif

    return pos;
  }
}%

CCProgram sprite-fs %{
  precision highp float;
  #include <embedded-alpha>
  #include <alpha-test>

  in vec4 color;

  #if USE_TEXTURE
    in vec2 uv0;
    #pragma builtin(local)
    layout(set = 2, binding = 10) uniform sampler2D cc_spriteTexture;
  #endif

  vec4 frag () {
    vec4 o = vec4(1, 1, 1, 1);

    #if USE_TEXTURE
      o *= CCSampleWithAlphaSeparated(cc_spriteTexture, uv0);
      #if IS_GRAY
        float gray  = 0.2126 * o.r + 0.7152 * o.g + 0.0722 * o.b;
        o.r = o.g = o.b = gray;
      #endif
    #endif

    o *= color;
    ALPHA_TEST(o);
    return o;
  }
}%
