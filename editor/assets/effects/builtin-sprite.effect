// Copyright (c) 2017-2020 Xiamen Yaji Software Co., Ltd.
CCEffect %{
  techniques:
  - passes:
    - vert: sprite-vs:vert
      frag: sprite-fs:frag
      depthStencilState:
        depthTest: false
        depthWrite: false
      blendState:
        targets:
        - blend: true
          blendSrc: src_alpha
          blendDst: one_minus_src_alpha
          blendDstAlpha: one_minus_src_alpha
      rasterizerState:
        cullMode: none
      properties:
        alphaThreshold: { value: 0.5 }
}%

CCProgram sprite-vs %{
  precision highp float;
  #include <cc-global>
  #define CC_UI_PER_BATCH 16

  in vec3 a_position;
  in vec2 a_texCoord;
  in float a_batch_id;

  out vec4 color;
  out vec2 uv0;

  struct UIData {
    vec4 rotation;
    vec4 tilingOffset;
    vec4 color;
    vec3 translation;
    vec3 scale;
  };

  #pragma builtin(local)
  layout(set = 2, binding = 0) uniform CCUILocal {
    highp vec4 cc_local_data1[CC_UI_PER_BATCH]; // xyz: translation w: color R.G
    highp vec4 cc_local_data2[CC_UI_PER_BATCH]; // xyzw: quat rotation
    highp vec4 cc_local_data3[CC_UI_PER_BATCH]; // xy: scale zw: color BA
    highp vec4 cc_local_data4[CC_UI_PER_BATCH]; // xy: tiling zw: offset
  };

  void getUIData(out UIData o) {
    int instanceID = int(a_batch_id);
    vec4 data1 = cc_local_data1[instanceID];
    vec4 data3 = cc_local_data3[instanceID];
    float rg = data1.w;
    float r = floor(rg);

    o.rotation = cc_local_data2[instanceID];
    o.tilingOffset = cc_local_data4[instanceID];
    o.translation = data1.xyz;
    o.scale = vec3(data3.xy, 1.0);
    o.color = vec4(r * (1.0 / 255.0), rg - r, data3.zw);
  }

  vec3 VectorTransformQuat (vec3 v, vec4 Q) {
    return v + 2.0 * cross(Q.xyz, cross(Q.xyz, v) + Q.w * v);
  }

  vec4 vert () {
    UIData i;
    getUIData(i);

    vec4 pos = vec4(VectorTransformQuat(a_position * i.scale * 80.0, i.rotation) + i.translation, 1.0);
    uv0 = a_texCoord * i.tilingOffset.xy + i.tilingOffset.zw;
    color = i.color;

    #if USE_PIXEL_ALIGNMENT
      pos = cc_matView * pos;
      pos.xyz = floor(pos.xyz);
      pos = cc_matProj * pos;
    #else
      pos = cc_matViewProj * pos;
    #endif

    return pos;
  }
}%

CCProgram sprite-fs %{
  precision highp float;
  #include <embedded-alpha>
  #include <alpha-test>

  in vec4 color;

  #if USE_TEXTURE
    in vec2 uv0;
    #pragma builtin(local)
    layout(set = 2, binding = 10) uniform sampler2D cc_spriteTexture;
  #endif

  vec4 frag () {
    vec4 o = vec4(1, 1, 1, 1);

    #if USE_TEXTURE
      o *= CCSampleWithAlphaSeparated(cc_spriteTexture, uv0);
      #if IS_GRAY
        float gray  = 0.2126 * o.r + 0.7152 * o.g + 0.0722 * o.b;
        o.r = o.g = o.b = gray;
      #endif
    #endif

    o *= color;
    ALPHA_TEST(o);
    return o;
  }
}%
