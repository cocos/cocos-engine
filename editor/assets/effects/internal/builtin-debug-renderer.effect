// Copyright (c) 2017-2020 Xiamen Yaji Software Co., Ltd.
CCEffect %{
  techniques:
  - passes:
    - vert: debug-renderer-vs
      frag: debug-renderer-fs
      pass: debug-profiler
      priority: max
      depthStencilState:
        depthTest: false
        depthWrite: false
      blendState:
        targets:
        - blend: true
          blendSrc: src_alpha
          blendDst: one_minus_src_alpha
          blendDstAlpha: one_minus_src_alpha
      rasterizerState:
        cullMode: none
}%

CCProgram debug-renderer-vs %{
  precision mediump float;
  #pragma rate CCProfiler pass
  layout(std140, set = 0, binding = 0) uniform CCProfiler {
    mediump vec4 cc_surfaceTransform; // 0: orientation, 1: flip
    mediump vec4 cc_screenSize;
  };

  #define CC_HANDLE_GET_CLIP_FLIP(uv) uv = cc_surfaceTransform.y == 0.0 ? vec2(uv.x, -uv.y) : uv

  in vec2 a_position;
  in vec2 a_texCoord;
  in vec4 a_color;

  out vec2 v_texCoord;
  out vec4 v_color;

  void main () {
    int orientation = int(cc_surfaceTransform.x);
    vec4 transform = vec4(1.0, 0.0, 0.0, 1.0);

    if (orientation == 0) {
      transform = vec4(1.0, 0.0, 0.0, 1.0);
    } else if (orientation == 1) {
      transform = vec4(0.0, 1.0, -1.0, 0.0);
    } else if (orientation == 2) {
      transform = vec4(-1.0, 0.0, 0.0, -1.0);
    } else if (orientation == 3) {
      transform =  vec4(0.0, -1.0, 1.0, 0.0);
    }
    vec2 invScreenSize = (orientation == 1 || orientation == 3) ? cc_screenSize.wz : cc_screenSize.zw;
    vec2 position = a_position * invScreenSize;
    position = position * vec2(2.0, -2.0) + vec2(-1.0, 1.0);

    vec2 clipPos = vec2(dot(transform.xy, position), dot(transform.zw, position));
    CC_HANDLE_GET_CLIP_FLIP(clipPos);

    v_texCoord = a_texCoord;
    v_color = a_color;

    gl_Position = vec4(clipPos, 0.0, 1.0);
  }
}%

CCProgram debug-renderer-fs %{
  precision mediump float;

  in vec2 v_texCoord;
  in vec4 v_color;

  uniform sampler2D mainTexture;

  layout(location = 0) out vec4 outColor;

  void main () {
    outColor = vec4(v_color.rgb, v_color.a * texture(mainTexture, v_texCoord).r);
  }
}%
