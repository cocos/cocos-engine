// Copyright (c) 2017-2020 Xiamen Yaji Software Co., Ltd.
CCEffect %{
  techniques:
  - name: color-grading
    passes:
    - vert: color-grading-vs:vert
      frag: color-grading-fs:frag
      pass: color-grading
      depthStencilState:
        depthTest: false
        depthWrite: false
      rasterizerState:
        cullMode: none
}%

CCProgram color-grading-vs %{
  precision highp float;
  #include <builtin/uniforms/cc-global>

  in vec2 a_position;
  in vec2 a_texCoord;
  out vec2 v_uv;

  vec4 vert () {
    vec4 pos = vec4(a_position, 0.0, 1.0);
    v_uv = a_texCoord;
    return pos;
  }
}%


CCProgram color-grading-fs %{
  precision highp float;
  #include <common/color/gamma>

  uniform fxaaUBO {
    vec4 texSize;
  };

  in vec2 v_uv;
  #pragma rate sceneColorMap pass
  uniform sampler2D sceneColorMap;
  uniform sampler2D colorGradingMap;

  #define COLORS 32.0
  #define COLOR_GRADING_SCALE  vec3((COLORS - 1.0) / (COLORS * COLORS), (COLORS - 1.0) / COLORS, 1.0 / COLORS)
  #define COLOR_GRADING_OFFSET vec2(1.0 / (2.0 * COLORS * COLORS), 1.0 / (2.0 * COLORS))

  #define CONTRIBUTE 1.0

  vec3 ColorGrading(sampler2D lutTexture, vec3 color) {
    vec2 uv = vec2(color.r, 1.0 - color.g) * COLOR_GRADING_SCALE.xy + COLOR_GRADING_OFFSET;
    float b = color.b * (COLORS - 1.0);
    vec2 lr = floor(vec2(b, b + 1.0));
    float lerp = b - lr.x;
    lr *= COLOR_GRADING_SCALE.z;
    vec3 left = texture(lutTexture, uv + vec2(lr.x, 0.0)).rgb;
    vec3 right = texture(lutTexture, uv + vec2(lr.y, 0.0)).rgb;
    return mix(left, right, lerp);
  }

  vec4 frag () {
    vec4 sceneColor = texture(sceneColorMap, v_uv);
    vec3 gradeColor = ColorGrading(colorGradingMap, sceneColor.rgb);
    return mix(sceneColor, vec4(gradeColor, 1.0), CONTRIBUTE);
  }
}%
